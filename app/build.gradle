apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'


def cprops = new Properties()
cprops.load(new FileInputStream(file('config.properties')))
def config = new ConfigSlurper().parse(cprops)

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion
    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        dataBinding = true
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "env"
    productFlavors {
        development {
            dimension "env"
            applicationId "com.abrahamlay.covid19app.dev"
            buildConfigField("String", "API_BASE_URL", config.env.dev.baseurl)
        }
        staging {
            dimension "env"
            applicationId "com.abrahamlay.covid19app.dev"
            buildConfigField("String", "API_BASE_URL", config.env.staging.baseurl)
        }
        production {
            dimension "env"
            applicationId 'com.abrahamlay.covid19app'
            buildConfigField("String", "API_BASE_URL", config.env.prod.baseurl)
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation kotlinStdLib

    implementation project(':domain')
    implementation project(':data')
    implementation project(':base')
    implementation project(':home')

    //database
    implementation support.room
    kapt support.roomCompiler

    //androidx
    implementation support.appcompat
    implementation support.constraint
    implementation support.recyclerview
    implementation support.core
    implementation support.design

    //dependency injection
    implementation support.koin
    implementation support.koinScope
    implementation support.koinViewmodel

    //network
    implementation libs.retrofit
    implementation libs.okhttp
    implementation libs.gson

    testImplementation test.jUnit
    androidTestImplementation test.testRunner
    androidTestImplementation test.espresso
}
